title = "Glycolyse";

geometry = 120:40;

molecule Gl, Frpp, Gap, Py; // les substrats et produits
molecule HPI2, Ad2, PEP2; // les enzymes
molecule ATP2; // ATP

speed(ATP2) = 1;
speed(Gl) = 0.1;
speed(Frpp) = 0.1;
speed(Gap) = 0.2;
speed(Py) = 0.2;
speed(HPI2) = 0.02;
speed(Ad2) = 0.02;
speed(PEP2) = 0.02;


maxlinks(Gl) = HPI2(1);
maxlinks(Frpp) = HPI2(1), Ad2(1);
maxlinks(Gap) = Gap(1),Ad2(1),PEP2(1);
maxlinks(Py) = PEP2(1),ATP2(1);
maxlinks(HPI2) = Gl(1), Frpp(1);
maxlinks(Ad2) = Frpp(1),Gap(1);
maxlinks(PEP2) = Gap(1),Py(1);
maxlinks(ATP2) = Py(1);

Gl+HPI2 -> Gl*HPI2 [1];
Gl*HPI2 -> Gl+HPI2 [0.1];

{HPI2}Gl + ATP2 -> Frpp [1]; //HPI2 entre guillemets car celui pas entre guillement est celui qui devient Frpp, et Frpp est implicitement complexé avec HPI
HPI2*Frpp->HPI2+Frpp [1]; //Ici on déconnecte HPI de Frpp qui étaient connecté implicitement

{~HPI2}Frpp+Ad2->Frpp*Ad2 [0.1]; 
Frpp*Ad2->Frpp+Ad2 [0.01];
{Ad2}Frpp->Gap*Gap [0.1]; //on met 2 gap car on en produit par 2 voies de métabo, mais pour qu'il comprenne que les deux sont complexés à Ad2, on doit mettre gap*gap
Ad2*Gap->Ad2+Gap [1];
Gap * Gap -> Gap + Gap [0.5]; //très important car gap restent complexés entre eux meme après avoir été décompléxés de l'enzyme
{~Ad2}Gap+PEP2->Gap*PEP2 [0.1];
Gap*PEP2->Gap+PEP2 [0.01];
{PEP2}Gap->Py*ATP2 [0.5];
Py*ATP2->Py+ATP2 [1]; //On décomplexe d'abord Py et ATP2, ATP2 part tout seul et Py part avec PEP2
PEP2*Py->PEP2+Py [1]; //On décomplexe Py de PEP2

init(800,Gl);
init(200,ATP2);
init(200,HPI2);
init(200,Ad2);
init(200,PEP2);

